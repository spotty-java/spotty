plugins {
    id 'maven-publish'
    id 'java-test-fixtures'
}

dependencies {
    implementation "org.apache.tika:tika-core:$apacheTika"

    testImplementation "org.apache.commons:commons-lang3:$apacheCommons"
    testImplementation "org.apache.httpcomponents:httpclient:$apacheHttpClientVersion"
    testImplementation "org.awaitility:awaitility:$awaitilityVersion"
}

java {
    // create jar with sources to publish it
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            // disable publish test fixtures
            components.java.withVariantsFromConfiguration(configurations.testFixturesApiElements) { skip() }
            components.java.withVariantsFromConfiguration(configurations.testFixturesRuntimeElements) { skip() }

            from components.java
        }
    }
}

// generate SpottyVersion file
def spottySrc = "$buildDir/generated/sources/spotty/java/main"
sourceSets {
    main {
        java {
            srcDir spottySrc
        }
    }
}

task spottyVersionTask {
    doLast {
        def file = file("$spottySrc/spotty/version/SpottyVersion.java")
        if (!file.exists()) {
            file.parentFile.mkdirs()
        }

        file.text = """
            package spotty.version;
        
            public final class SpottyVersion { 
                public static final String VERSION = "$version"; 
            }
        """.stripIndent().trim()
    }
}

compileJava.dependsOn spottyVersionTask