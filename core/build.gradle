plugins {
    id 'maven-publish'
    id 'java-test-fixtures'
}

dependencies {
    implementation "org.apache.tika:tika-core:$apacheTika"

    testImplementation "org.apache.commons:commons-lang3:$apacheCommons"
    testImplementation "org.apache.httpcomponents:httpclient:$apacheHttpClientVersion"
    testImplementation "org.awaitility:awaitility:$awaitilityVersion"
    testImplementation "com.konghq:unirest-java:$unirestVersion"
}

java {
    // create jar with sources to publish it
    withSourcesJar()
}

publishing {
    repositories {
        maven {
            url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = System.getenv("MAVEN_REPOSITORY_USERNAME")
                password = System.getenv("MAVEN_REPOSITORY_PASSWORD")
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            // disable publish test fixtures
            components.java.withVariantsFromConfiguration(configurations.testFixturesApiElements) { skip() }
            components.java.withVariantsFromConfiguration(configurations.testFixturesRuntimeElements) { skip() }

            from components.java

            pom {
                name = "Spotty"
                description = "Fast lightweight java web server"
                url = "https://github.com/spotty-java/spotty"

                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }

                developers {
                    developer {
                        id = "lazer-dev"
                        name = "Alex Danilenko"
                        email = "alex.danilenko.dev@gmail.com"
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/spotty-java/spotty.git'
                    developerConnection = 'scm:git:ssh://github.com/spotty-java/spotty.git'
                    url = 'https://github.com/spotty-java/spotty'
                }
            }
        }
    }
}

// generate SpottyVersion file
def spottySrc = "$buildDir/generated/sources/spotty/java"
sourceSets {
    main {
        java {
            srcDir spottySrc
        }
    }
}

task spottyVersionTask {
    doLast {
        def file = file("$spottySrc/spotty/version/SpottyVersion.java")
        if (!file.parentFile.exists()) {
            file.parentFile.mkdirs()
        }

        file.text = """
            package spotty.version;
        
            public final class SpottyVersion { 
                public static final Version VERSION = Version.parse("$version"); 
            }
        """.stripIndent().trim()
    }
}

compileJava.dependsOn spottyVersionTask